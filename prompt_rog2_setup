#
# A simple theme inspired by the Damoekri theme.
#
# Authors:
#   Rogelio Alberto <i@rog.mx>
#
# Screenshots:
#   http://i.imgur.com/QUPfLfP.png
#

# Load dependencies.
pmodload 'helper'

function prompt_rog_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_rog_pwd="$MATCH"
    unset MATCH
  else
    _prompt_rog_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_rog_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_rog_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Node version information.
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_rog_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info and ruby-info before each command.
  add-zsh-hook precmd prompt_rog_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %F{green}»%f'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_rog_colors=(
      "%F{81}"  # Turquoise
      "%F{214}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
      "%F{27}"  # Tardis blue
      "%F{4}"   # White
    )
  else
    _prompt_rog_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{blue}"
      "%F{white}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_rog_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${_prompt_rog_colors[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${_prompt_rog_colors[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${_prompt_rog_colors[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${_prompt_rog_colors[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b %d%a%m%u'

  # Set node-info parameters.
  zstyle ':prezto:module:node:info:version' format ' %v'

  # Define prompts.
  PROMPT="${_prompt_rog_colors[7]}%n%f@${_prompt_rog_colors[6]}%m%f ${_prompt_rog_colors[3]}%~%f"'  %F${_prompt_rog_colors[7]}${git_info:+‣ ${(e)git_info[prompt]}} 
%B${user_symbol}%f${editor_info[keymap]} '
  # Short pwd
  # PROMPT="${_prompt_rog_colors[7]}%n%f@${_prompt_rog_colors[6]}%m%f "'%F${_prompt_rog_colors[3]}${_prompt_rog_pwd} %F${_prompt_rog_colors[7]}${git_info:+‣ ${(e)git_info[prompt]}} 
  # %B${user_symbol}%f${editor_info[keymap]} '
  RPROMPT='${_prompt_rog_colors[5]}⬡%f ${node_info[version]}'
  # Git info on the right
  # RPROMPT='${git_info:+${(e)git_info[prompt]} } ${_prompt_rog_colors[5]}⬡%f ${node_info[version]}'
}

prompt_rog_setup "$@"
